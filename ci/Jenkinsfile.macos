#!/usr/bin/env groovy
library 'status-jenkins-lib@v1.9.33'

/* Options section can't access functions in objects. */
def isPRBuild = utils.isPRBuild()

pipeline {
  /* This way we run the same Jenkinsfile on different platforms. */
  agent {
    label "${getAgentLabels().join(' && ')} && qt-6.9.2 && go-1.24 && xcode-16.2"
  }

  parameters {
    booleanParam(
      name: 'RELEASE',
      description: 'Decides whether release credentials are used.',
      defaultValue: params.RELEASE ?: false
    )
    booleanParam(
      name: 'INCLUDE_DEBUG_SYMBOLS',
      description: 'Decides whether binaries are built with debug symbols.',
      defaultValue: params.INCLUDE_DEBUG_SYMBOLS ?: false
    )
    choice(
      name: 'VERBOSE',
      description: 'Level of verbosity based on nimbus-build-system setup.',
      choices: ['0', '1', '2']
    )
    booleanParam(
      name: 'USE_MOCKED_KEYCARD_LIB',
      description: 'Decides whether the mocked status-keycard-go library is built.',
      defaultValue: false
    )
    choice(
      name: 'ENTITLEMENTS',
      description: 'Select app entitlements. Squish requires extra entitlements.',
      choices: ['resources/Entitlements.plist', 'resources/Entitlements_squish.plist']
    )
    booleanParam(
      name: 'USE_NWAKU',
      description: 'Whether to build with nwaku or not',
      defaultValue: params.USE_NWAKU ?: getNWakuMode(),
    )
    string(
      name: 'NIMFLAGS',
      description: 'Extra Nim flags. Examples: --verbosity:2 --passL:"-v" --passC:"-v"',
      defaultValue: "--colors:off --nimcache:${env.WORKSPACE_TMP}/nimcache"
    )
  }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 30, unit: 'MINUTES')
    /* manage how many builds we keep */
    buildDiscarder(logRotator(
      numToKeepStr: '10',
      daysToKeepStr: '30',
      artifactNumToKeepStr: '1',
    ))
    /* Allows combined build to copy */
    copyArtifactPermission('/status-app/*')
    /* Abort old PR builds. */
    disableConcurrentBuilds(
      abortPrevious: isPRBuild
    )
    disableRestartFromStage()
  }

  environment {
    PLATFORM = "macos/${getArch()}${(params.USE_NWAKU ?: false) ? '-nwaku' : ''}"
    /* Improve make performance */
    MAKEFLAGS = "-j${utils.getProcCount()} V=${params.VERBOSE}"
    QT_VERSION="6.9.2"
    QMAKE = "/Users/admin/${QT_VERSION}/macos/bin/qmake"
    /* QMAKE = sh(script: "which qmake", returnStdout: true).trim() */
    QTDIR = sh(script:"${env.QMAKE} -query QT_INSTALL_PREFIX", returnStdout: true).trim()
    /* Enforce Go version installed infra-role-golang. */
    /* to fix missing rcc, since QT6 rcc is located at ${QTDIR}/libexec/rcc */
    PATH = "${env.QTDIR}/bin:${env.QTDIR}/libexec:${env.HOME}/go/bin:/usr/local/go/bin:${env.PATH}"
    /* Avoid weird bugs caused by stale cache. */
    QML_DISABLE_DISK_CACHE = "true"
    /* Set USE_NWAKU before VERSION since version.sh uses it */
    USE_NWAKU = "${params.USE_NWAKU}"
    /* nwaku source directory */
    NWAKU_SOURCE_DIR = "${env.WORKSPACE_TMP}/nwaku"
    /* nim-sds source directory */
    NIM_SDS_SOURCE_DIR = "${env.WORKSPACE_TMP}/nim-sds"
    /* Controls the App version in Settings */
    VERSION = sh(script: "./scripts/version.sh", returnStdout: true).trim()
    /* Control output the filename */
    APP_TYPE = "${utils.getAppType() + ( params.USE_NWAKU ? '-experimental' : '' )}"
    STATUS_CLIENT_DMG = "pkg/${utils.pkgFilename(ext: 'dmg', arch: getArch(), version: env.VERSION, type: env.APP_TYPE)}"
    /* Apple Team ID for Notarization */
    MACOS_NOTARIZE_TEAM_ID = "8B5X2M6H2Y"
    /* prevent sharing cache dir across different jobs */
    GO_GENERATE_FAST_DIR = "${env.WORKSPACE_TMP}/go-generate-fast"
    SENTRY_PRODUCTION = "${utils.isReleaseBuild() ? 'true' : 'false'}"
    /* Hack fix for params not being set in job on first run */
    NIMFLAGS =               "${params.NIMFLAGS}"
    ENTITLEMENTS =           "${params.ENTITLEMENTS}"
    RELEASE =                "${params.RELEASE}"
    INCLUDE_DEBUG_SYMBOLS =  "${params.INCLUDE_DEBUG_SYMBOLS}"
    VERBOSE =                "${params.VERBOSE}"
    USE_MOCKED_KEYCARD_LIB = "${params.USE_MOCKED_KEYCARD_LIB}"
    /* FIXME: figure out why NIMFLAGS are not respected */
    XDG_CACHE_HOME = "${env.WORKSPACE_TMP}/.cache"
  }

  stages {
    stage('Cleanup Workspace') {
      steps {
        sh './scripts/clean-git.sh'
      }
    }
    stage('Fetch submodules') {
      steps {
        sh 'git submodule update --init --recursive'
      }
    }
    stage('Deps') {
      steps {
        sh 'make update'
        withCredentials([
          usernamePassword( /* For fetching HomeBrew bottles. */
            credentialsId: "status-im-auto-pkgs",
            usernameVariable: 'GITHUB_USER',
            passwordVariable: 'GITHUB_TOKEN'
          )
        ]) {
          sh 'make deps'
        }
      }
    }

    stage('status-go') {
      steps {
        sh 'make status-go'
      }
    }

    stage('Package') {
      steps { script {
        macos.bundle('pkg-macos')
      } }
    }

    stage('Notarize') {
      when { expression { utils.isReleaseBuild() } }
      steps { script {
        macos.notarize()
      } }
    }

    stage('Parallel Upload') {
      parallel {
        stage('Upload') {
          steps { script {
            env.PKG_URL = s5cmd.upload(env.STATUS_CLIENT_DMG)
            jenkins.setBuildDesc(Dmg: env.PKG_URL)
          } }
        }
        stage('Archive') {
          steps { script {
            archiveArtifacts(env.STATUS_CLIENT_DMG)
          } }
        }
      }
    }
  }
  post {
    success { script { github.notifyPR(true) } }
    failure { script { github.notifyPR(false) } }
    cleanup {
      cleanWs(disableDeferredWipeout: true)
      dir(env.WORKSPACE_TMP) { deleteDir() }
    }
  }
}

/* This allows us to use one Jenkinsfile and run
 * jobs on different platforms based on job name. */
def getAgentLabels() {
    if (params.AGENT_LABEL) { return params.AGENT_LABEL }
    /* We extract the name of the job from currentThread because
     * before an agent is picket env is not available. */
    def tokens = Thread.currentThread().getName().split('/')
    def labels = []
    /* Check if the job path contains any of the valid labels. */
    ['linux', 'macos', 'x86_64', 'aarch64', 'arm64'].each {
        if (tokens.contains(it)) { labels.add(it) }
    }
    return labels
}

def getArch() {
    def tokens = Thread.currentThread().getName().split('/')
    for (def arch in ['x86_64', 'aarch64']) {
      if (tokens.contains(arch)) { return arch }
    }
}

/* We extract the name of the job from currentThread because
 * before an agent is picked env is not available. */
def getJobPathTokens() {
    return Thread.currentThread().getName().split('/')
}

def getNWakuMode() {
    return getJobPathTokens().contains('package-nwaku')
}
