library 'status-react-jenkins@v1.2.4'

pipeline {
  agent {
    label 'macos'
  }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 15, unit: 'MINUTES')
    /* manage how many builds we keep */
    buildDiscarder(logRotator(
      numToKeepStr: '20',
      daysToKeepStr: '60',
    ))
  }

  environment {
    TARGET = 'macos'
    /* Improve make performance */
    MAKEFLAGS = '-j4'
    /* Disable colors in Nim compiler logs */
    NIMFLAGS = '--colors:off'
    /* Qt location is pre-defined */
    QTDIR = '/usr/local/qt/clang_64'
    PATH = "${env.QTDIR}/bin:${env.PATH}"
    /* Control output the filename */
    STATUS_CLIENT_DMG = "pkg/${utils.pkgFilename('dmg')}"
  }

  stages {
    stage('Deps') {
      steps { 
        /* avoid re-compiling Nim by using cache */
        cache(maxCacheSize: 250, caches: [[
          $class: 'ArbitraryFileCache',
          includes: '**/*',
          path: 'vendor/nimbus-build-system/vendor/Nim/bin'
        ]]) {
          sh 'make deps'
        }
      }
    }

    stage('status-go') {
      steps { sh 'make status-go' }
    }

    stage('Client') {
      steps { 
        withCredentials([string(
          credentialsId: utils.getInfuraTokenCred(),
          variable: 'INFURA_TOKEN'
        )]) {
          sh 'make nim_status_client'
        }
      }
    }

    stage('Package') { steps {
      withCredentials([
        string(
          credentialsId: 'macos-keychain-identity',
          variable: 'MACOS_CODESIGN_IDENT'
        ),
        string(
          credentialsId: 'macos-keychain-pass',
          variable: 'MACOS_KEYCHAIN_PASS'
        ),
        file(
          credentialsId: 'macos-keychain-file',
          variable: 'MACOS_KEYCHAIN_FILE'
        ),
      ]) {
        sh 'make pkg-macos'
      }
    } }

    stage('Parallel Upload') {
      parallel {
        stage('Upload') {
          steps { script {
            env.PKG_URL = s3.uploadArtifact(env.STATUS_CLIENT_DMG)
            jenkins.setBuildDesc(Dmg: env.PKG_URL)
          } }
        }
        stage('Archive') {
          steps { script {
            archiveArtifacts(env.STATUS_CLIENT_DMG)
          } }
        }
      }
    }
  }
  post {
    success { script { github.notifyPR(true) } }
    failure { script { github.notifyPR(false) } }
    always { cleanWs() }
  }
}
