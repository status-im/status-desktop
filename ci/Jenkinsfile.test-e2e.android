#!/usr/bin/env groovy
library 'status-jenkins-lib@v1.9.27'

pipeline {
  agent {
    dockerfile {
      label 'linuxcontainer'
      filename 'tests.Dockerfile'
      dir 'ci'
      args '--user jenkins'
    }
  }

  parameters {
    gitParameter(
      name: 'GIT_REF',
      description: 'Git branch to checkout.',
      branchFilter: 'origin/(.*)',
      branch: '',
      defaultValue: 'master',
      quickFilterEnabled: false,
      selectedValue: 'DEFAULT',
      sortMode: 'ASCENDING_SMART',
      tagFilter: '*',
      type: 'PT_BRANCH'
    )
    string(
      name: 'BUILD_SOURCE',
      description: 'URL to APK or Jenkins build (pkg/*.apk). Required when BROWSERSTACK_APP_ID is empty.',
      defaultValue: ''
    )
    string(
      name: 'BROWSERSTACK_APP_ID',
      description: 'Existing BrowserStack app identifier (bs://...). Leave empty to upload from BUILD_SOURCE.',
      defaultValue: ''
    )
    choice(
      name: 'TEST_DEVICE_ID',
      description: 'BrowserStack device id to run on (leave empty to use the environment default).',
      choices: ['', 'galaxy_tab_s10p_android_15', 'pixel_8_android_14', 'pixel_7_android_13', 'samsung_s23_android_13']
    )
    string(
      name: 'PYTEST_ARGS',
      description: 'Pytest flags (e.g. "-m smoke" or "-m critical") and other args.',
      defaultValue: '-n=5 -m smoke tests'
    )
  }

  options {
    timestamps()
    timeout(time: 120, unit: 'MINUTES')
    buildDiscarder(logRotator(
      daysToKeepStr: '30',
      numToKeepStr: '30',
      artifactNumToKeepStr: '30',
    ))
    disableRestartFromStage()
  }

  environment {
    VIRTUAL_ENV = "${env.WORKSPACE_TMP}/venv-appium"
    PYTHONUNBUFFERED = "1"
    TEST_DEVICE_ID = "${params.TEST_DEVICE_ID}"
    BROWSERSTACK_PROJECT_NAME = "Mobile E2E ${utils.getBuildType()}"
  }

  stages {
    stage('Prep') {
      steps {
        script {
          setNewBuildName()
          updateGitHubStatus()
        }
      }
    }

    stage('Setup Python environment') {
      steps {
        dir('test/e2e_appium') {
          sh """
            set -euo pipefail
            python3 -m venv ${VIRTUAL_ENV}
            source ${VIRTUAL_ENV}/bin/activate
            pip install --upgrade pip
            pip install -r requirements.txt
          """
        }
      }
    }

    stage('Use provided BrowserStack app') {
      when {
        expression { params.BROWSERSTACK_APP_ID?.trim() }
      }
      steps {
        script {
          env.BROWSERSTACK_APP_ID = params.BROWSERSTACK_APP_ID.trim()
          echo "Using provided BrowserStack app id: ${env.BROWSERSTACK_APP_ID}"
        }
      }
    }

    stage('Download') {
      when {
        allOf {
          expression { !params.BROWSERSTACK_APP_ID?.trim() }
          expression { params.BUILD_SOURCE?.startsWith('http') }
        }
      }
      steps { timeout(5) { script { dir('test/e2e_appium') {
        if (!params.BUILD_SOURCE?.trim()) {
          error('Specify BUILD_SOURCE when BROWSERSTACK_APP_ID is empty.')
        }
        sh 'mkdir -p ./pkg/'
        fileOperations([
          fileDownloadOperation(
            url:            params.BUILD_SOURCE,
            targetFileName: 'downloaded.apk',
            targetLocation: './pkg/',
            userName:       '',
            password:       '',
          )
        ])
        def apkPath = utils.findFile('test/e2e_appium/pkg/*.apk')
        if (!apkPath) {
          error("Unable to locate APK under pkg/. Ensure BUILD_SOURCE produces pkg/*.apk artifacts.")
        }
        env.APK_PATH = apkPath
        echo "APK ready at ${env.APK_PATH}"
      } } } }
    }

    stage('Copy') {
      when {
        allOf {
          expression { !params.BROWSERSTACK_APP_ID?.trim() }
          expression { !params.BUILD_SOURCE?.startsWith('http') }
        }
      }
      steps { timeout(5) { script { dir('test/e2e_appium') {
        if (!params.BUILD_SOURCE?.trim()) {
          error('Specify BUILD_SOURCE when BROWSERSTACK_APP_ID is empty.')
        }
        copyArtifacts(
          projectName: params.BUILD_SOURCE,
          filter:      'pkg/*.apk',
          selector:    lastWithArtifacts(),
          target:      './'
        )
        def apkPath = utils.findFile('test/e2e_appium/pkg/*.apk')
        if (!apkPath) {
          error("Unable to locate APK under pkg/. Ensure BUILD_SOURCE produces pkg/*.apk artifacts.")
        }
        env.APK_PATH = apkPath
        echo "APK ready at ${env.APK_PATH}"
      } } } }
    }

    stage('Upload APK to BrowserStack') {
      when {
        expression { !env.BROWSERSTACK_APP_ID }
      }
      steps {
        script {
          withCredentials([
            usernamePassword(
              credentialsId: 'browserstack-status-desktop',
              usernameVariable: 'BROWSERSTACK_USERNAME',
              passwordVariable: 'BROWSERSTACK_ACCESS_KEY'
            )
          ]) {
            def response = sh(
              script: "./scripts/upload_browserstack_apk.sh",
              returnStdout: true
            ).trim()
            def result = readJSON text: response
            def appUrl = result?.app_url
            if (!appUrl) {
              error("BrowserStack upload failed: ${response}")
            }
            env.BROWSERSTACK_APP_ID = appUrl
            env.BROWSERSTACK_BUILD_NAME = result?.custom_id
            env.BROWSERSTACK_BUILD_IDENTIFIER = env.BUILD_NUMBER
            echo "BrowserStack app uploaded: ${env.BROWSERSTACK_APP_ID}"
          }
        }
      }
    }

    stage('Run pytest suite') {
      steps {
        script {
          dir('test/e2e_appium') {
            println("Using TEST_DEVICE_ID: ${env.TEST_DEVICE_ID}")
            
            withCredentials([
              usernamePassword(
                credentialsId: 'browserstack-status-desktop',
                usernameVariable: 'BROWSERSTACK_USERNAME',
                passwordVariable: 'BROWSERSTACK_ACCESS_KEY'
              )
            ]) {
              sh "${VIRTUAL_ENV}/bin/python -m pytest --env browserstack ${params.PYTEST_ARGS?.trim() ?: ''}"
            }
          }
        }
      }
    }

    stage('Publish test results') {
      steps {
        script {
          def runId = env.E2E_RUN_ID?.trim()
          def reportsPattern = runId ? "test/e2e_appium/reports/${runId}/**/*.xml" : "test/e2e_appium/reports/**/*.xml"
          junit allowEmptyResults: true, testResults: reportsPattern
          def archivePattern = runId ? "test/e2e_appium/reports/${runId}/**/*" : "test/e2e_appium/reports/**/*"
          archiveArtifacts artifacts: archivePattern, allowEmptyArchive: true
        }
      }
    }
  }

  post {
    success {
      script {
        github.notifyPR(true)
      }
    }
    failure {
      script {
        github.notifyPR(false)
      }
    }
    cleanup {
      cleanWs(disableDeferredWipeout: true)
    }
  }
}

def setNewBuildName() {
  if (currentBuild.upstreamBuilds) {
    def parent = utils.parentOrCurrentBuild()
    currentBuild.displayName = parent.getFullDisplayName().minus('status-desktop Â» ')
  }
}

def updateGitHubStatus() {
  if (params.BUILD_SOURCE ==~ /.*\/PR-[0-9]+\/?$/) {
    github.statusUpdate(
      context: 'jenkins/prs/tests/e2e-android',
      commit: jenkins.getJobCommitByPath(params.BUILD_SOURCE),
      repo_url: 'https://github.com/status-im/status-desktop'
    )
  }
}

