#!/usr/bin/env groovy
library 'status-jenkins-lib@fix-android-signing'

/* Options section can't access functions in objects. */
def isPRBuild = utils.isPRBuild()
def isNightlyBuild = utils.isNightlyBuild()
def isReleaseBranch = (env.CHANGE_BRANCH ?: env.BRANCH_NAME)?.startsWith('release')
def isReleaseBuild = utils.isReleaseBuild()

pipeline {
  agent {
    /* Image with Ubuntu 22.04, QT 6.9.2, Android SDK/NDK, Go, and Nim */
    docker {
      label 'linuxcontainer'
      image 'harbor.status.im/status-im/status-desktop-build:1.0.8-qt6.9.2-android'
      alwaysPull true
      args '--entrypoint="" ' +
           '--volume=/nix:/nix ' +
           '--volume=/etc/nix:/etc/nix '
      customWorkspace "/home/jenkins/workspace/status-app-android/${env.BUILD_NUMBER}"
    }
  }

  parameters {
    booleanParam(
      name: 'RELEASE',
      description: 'Decides whether release credentials are used.',
      defaultValue: params.RELEASE ?: false
    )
    choice(
      name: 'VERBOSE',
      description: 'Level of verbosity based on nimbus-build-system setup.',
      choices: ['0','1','2','3']
    )
    choice(
      name: 'PACKAGE_TYPE',
      description: 'Package type to build.',
      choices: ['auto', 'apk', 'aab']
    )
  }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 45, unit: 'MINUTES')
    /* manage how many builds we keep */
    buildDiscarder(logRotator(
      numToKeepStr: '10',
      daysToKeepStr: '30',
      artifactNumToKeepStr: '3',
    ))
    /* Allows combined build to copy */
    copyArtifactPermission('/status-app/*')
    /* Abort old PR builds. */
    disableConcurrentBuilds(abortPrevious: isPRBuild)
    disableRestartFromStage()
  }

  environment {
    GOCACHE = "${env.WORKSPACE_TMP}/go-cache"
    GOMODCACHE = "${env.WORKSPACE_TMP}/go-mod-cache"
    GOTMPDIR = "${env.WORKSPACE_TMP}"
    /* sets App Version in Settings */
    VERSION = sh(script: "./scripts/version.sh", returnStdout: true).trim()
    /* Control output the filename */
    APP_TYPE = "${utils.getAppType()}"
    PLATFORM = "android/arm64"
    MAKEFLAGS = "-j${utils.getProcCount()} V=${params.VERBOSE}"
    QT_VERSION = "6.9.2"
    QT_ANDROID_PATH = "/opt/qt/${env.QT_VERSION}/android_arm64_v8a"
    QMAKE = "/opt/qt/${env.QT_VERSION}/android_arm64_v8a/bin/qmake"
    /* override package type if set, otherwise auto-select based on branch */
    PACKAGE_TYPE = "${params.PACKAGE_TYPE != 'auto' ? params.PACKAGE_TYPE : (isReleaseBranch ? 'aab' : 'apk')}"
    /* BUILD_VARIANT controls package name and signing: pr = app.status.mobile.pr, release = app.status.mobile */
    BUILD_VARIANT = "${isReleaseBuild ? 'release' : 'pr'}"
    /* App name derived from BUILD_VARIANT */
    STATUS_ANDROID_APP_NAME = "${['pr': 'StatusPR', 'release': 'Status'][env.BUILD_VARIANT]}"
    STATUS_ARTIFACT = "pkg/${utils.pkgFilename(ext: env.PACKAGE_TYPE, arch: 'arm64', version: env.VERSION, type: env.APP_TYPE)}"
    STATUS_BINARY = "${WORKSPACE}/mobile/bin/android/qt6/${env.STATUS_ANDROID_APP_NAME}.${env.PACKAGE_TYPE}"
    /* For release builds, also produce AAB (unless already building AAB) */
    BUILD_AAB = "${isReleaseBuild && params.PACKAGE_TYPE != 'aab'}"
    STATUS_AAB_ARTIFACT = "pkg/${utils.pkgFilename(ext: 'aab', arch: 'arm64', version: env.VERSION, type: env.APP_TYPE)}"
    STATUS_AAB_BINARY = "${WORKSPACE}/mobile/bin/android/qt6/${env.STATUS_ANDROID_APP_NAME}.aab"
    NIM_SDS_SOURCE_DIR = "${env.WORKSPACE_TMP}/nim-sds"
  }

  stages {

    stage('Cleanup Workspace') {
      steps {
        sh './scripts/clean-git.sh'
      }
    }

    stage('Fetch submodules') {
      steps {
        sh 'git submodule update --init --recursive'
      }
    }

    stage('Build Android') {
      steps {
        script {
          app.buildSignedAndroid(target='mobile-build PACKAGE_TYPE=' + env.PACKAGE_TYPE, verbose=params.VERBOSE)
        }
      }
    }

    stage('Build AAB') {
      when { expression { env.BUILD_AAB == 'true' } }
      steps {
        script {
          app.buildSignedAndroid(target='mobile-build PACKAGE_TYPE=aab', verbose=params.VERBOSE)
        }
      }
    }

    stage('Package') {
      steps {
        sh 'mkdir -p pkg'
        sh "cp ${env.STATUS_BINARY} ${env.STATUS_ARTIFACT}"
        sh "ls -la ${env.STATUS_ARTIFACT}"
        script {
          if (env.BUILD_AAB == 'true') {
            sh "cp ${env.STATUS_AAB_BINARY} ${env.STATUS_AAB_ARTIFACT}"
            sh "ls -la ${env.STATUS_AAB_ARTIFACT}"
          }
        }
      }
    }

    stage('Parallel Upload') {
      parallel {
        stage('Upload') {
          steps {
            script {
              env.PKG_URL = s5cmd.upload(env.STATUS_ARTIFACT)
            }
          }
        }

        stage('Upload AAB') {
          when { expression { env.BUILD_AAB == 'true' } }
            steps {
              script {
                env.AAB_URL = s5cmd.upload(env.STATUS_AAB_ARTIFACT)
              }
            }
        }  
      }  
    }

    stage('Finalize') {
      steps {
        script {
        /* Archive artifacts. */
          def artifacts = [env.STATUS_ARTIFACT]
            if (env.BUILD_AAB == 'true') {
              artifacts << env.STATUS_AAB_ARTIFACT
            }
          archiveArtifacts artifacts.join(', ')

          /* Set build description. */
          def desc = [(env.PACKAGE_TYPE.toUpperCase()): env.PKG_URL]
          if (env.AAB_URL) {
            desc.AAB = env.AAB_URL
          }
          jenkins.setBuildDesc(desc)
       }
      }
    } 
  }

  post {
    success { script { github.notifyPR(true) } }
    failure { script { github.notifyPR(false) } }
    cleanup {
      cleanWs(disableDeferredWipeout: true)
      dir(env.WORKSPACE_TMP) { deleteDir() }
    }
  }
}
