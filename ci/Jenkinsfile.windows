#!/usr/bin/env groovy
library 'status-jenkins-lib@fix/get-version'

/* Options section can't access functions in objects. */
def isPRBuild = utils.isPRBuild()

pipeline {
  agent { label 'windows && x86_64 && qt-5.15.2 && go-1.22' }

  parameters {
    booleanParam(
      name: 'RELEASE',
      description: 'Decides whether release credentials are used.',
      defaultValue: params.RELEASE ?: false
    )
    booleanParam(
      name: 'INCLUDE_DEBUG_SYMBOLS',
      description: 'Decides whether binaries are built with debug symbols.',
      defaultValue: params.INCLUDE_DEBUG_SYMBOLS ?: false
    )
    choice(
      name: 'VERBOSE',
      description: 'Level of verbosity based on nimbus-build-system setup.',
      choices: ['0', '1', '2']
    )
    string(
      name: 'NIMFLAGS',
      description: 'Extra Nim flags. Examples: --verbosity:2 --passL:"-v" --passC:"-v"',
      defaultValue: "--colors:off --nimcache:${env.WORKSPACE_TMP}/nimcache"
    )
    booleanParam(
      name: 'USE_MOCKED_KEYCARD_LIB',
      description: 'Decides whether the mocked status-keycard-go library is built.',
      defaultValue: false
    )
    choice(
      name: 'WINDOWS_CODESIGN_TIMESTAMP_URL',
      description: 'Time Stamp Authority (TSA) server for signing binaries.',
      choices: [
        'http://timestamp.digicert.com', /* Known to cause 0x80096005, 0x800700e1 errors. */
        'http://timestamp.apple.com/ts01',
        'http://timestamp.sectigo.com?td=sha256',
        'http://time.certum.pl',
        'https://freetsa.org',
      ]
    )
  }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 60, unit: 'MINUTES')
    /* manage how many builds we keep */
    buildDiscarder(logRotator(
      numToKeepStr: '10',
      daysToKeepStr: '30',
      artifactNumToKeepStr: '1',
    ))
    /* Allows combined build to copy */
    copyArtifactPermission('/status-desktop/*')
    /* Abort old PR builds. */
    disableConcurrentBuilds(
      abortPrevious: isPRBuild
    )
  }

  environment {
    PLATFORM = "windows/${getArch()}"
    /* Improve make performance */
    MAKEFLAGS = "-j${utils.getProcCount()} V=${params.VERBOSE}"
    /* Explicitly set the QT version */
    QTDIR = "/c/Qt/5.15.2/msvc2019_64"
    PATH = "${env.QTDIR}/bin:${goPath()}/bin:${env.PATH}"
    /* Avoid weird bugs caused by stale cache. */
    QML_DISABLE_DISK_CACHE = "true"
    /* Control output the filename */
    VERSION = sh(script: "./scripts/version.sh", returnStdout: true).trim()
    STATUS_CLIENT_EXE = "pkg/${utils.pkgFilename(ext: 'exe', arch: getArch(), version: env.VERSION)}"
    /* 7zip archive filename */
    STATUS_CLIENT_7Z = "pkg/${utils.pkgFilename(ext: '7z', arch: getArch(), version: env.VERSION)}"
    /* Hack-fix for params not being set in env on first job run. */
    WINDOWS_CODESIGN_TIMESTAMP_URL = "${params.WINDOWS_CODESIGN_TIMESTAMP_URL}"
    /* prevent sharing cache dir across different jobs */
    GO_GENERATE_FAST_DIR = "${env.WORKSPACE_TMP}/go-generate-fast"
    SENTRY_PRODUCTION = "${utils.isReleaseBuild() ? 'true' : 'false'}"
  }

  stages {
    stage('Cleanup Workspace') {
      steps {
        sh './scripts/clean-git.sh'
      }
    }
    stage('Deps') {
      steps {
        sh 'make update'
        sh 'make deps'
      }
    }

    stage('status-go') {
      steps {
        sh 'make status-go'
      }
    }

    stage('Package') {
      steps { script {
        windows.bundle("${env.STATUS_CLIENT_EXE} ${env.STATUS_CLIENT_7Z}")
      } }
    }

    stage('Parallel Upload') {
      /* Uploads on Windows are slow. */
      parallel {
        stage('Upload 7Z') {
          steps { script {
            zip_url = s5cmd.upload(env.STATUS_CLIENT_7Z)
          } }
        }
        stage('Upload EXE') {
          steps { script {
            exe_url = s5cmd.upload(env.STATUS_CLIENT_EXE)
          } }
        }
      }
    }

    stage('Archive') {
      steps { script {
        archiveArtifacts(env.STATUS_CLIENT_EXE)
        archiveArtifacts(env.STATUS_CLIENT_7Z)
        env.PKG_URL = exe_url
        jenkins.setBuildDesc(Zip: zip_url, Exe: exe_url)
      } }
    }
  }
  post {
    success { script { github.notifyPR(true) } }
    failure { script { github.notifyPR(false) } }
    // Windows workspace often becomes broken if stoped during checkout.
    // Post cleanup will fail too.
    // Use 'Wipe out repository and force clone' manual UI option to prevent it.
    cleanup { cleanWs() }
  }
}

def getArch() {
    def tokens = Thread.currentThread().getName().split('/')
    for (def arch in ['x86_64', 'aarch64']) {
      if (tokens.contains(arch)) { return arch }
    }
}

def goPath() {
  return sh(script: 'go env GOPATH', returnStdout: true).trim().replace('C:', '/c')
}
