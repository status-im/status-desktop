buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.8.0'
    }
}

repositories {
    google()
    mavenCentral()
}

apply plugin: qtGradlePluginType

configurations.all { config ->
    config.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    config.exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    //noinspection GradleDependency
    implementation 'androidx.core:core:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
}

// Allow overriding qtAndroidDir via environment variable (for different Qt installations)
def qtDir = System.getenv("QT_ANDROID_DIR") ?: qtAndroidDir

// Build variant: 'pr' or 'release' (controls app ID and name)
def buildVariant = System.getenv("BUILD_VARIANT") ?: "release"

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qtAndroidDir - holds the path to qt android files
     *                   needed to build any Qt application
     *                   on Android.
     * - qtGradlePluginType - whether to build an app or a library
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    namespace = androidPackageName
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion
    ndkVersion = androidNdkVersion

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qtDir + '/src', 'src', 'java']
            aidl.srcDirs = [qtDir + '/src', 'src', 'aidl']
            res.srcDirs = [qtDir + '/res', 'res']
            resources.srcDirs = ['resources']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError = false
    }

    // Do not compress Qt binary resources file
    aaptOptions {
        noCompress 'rcc'
    }

    defaultConfig {
        resConfig "en"
        minSdkVersion qtMinSdkVersion
        targetSdkVersion qtTargetSdkVersion
        ndk.abiFilters = qtTargetAbiList.split(",")
        // Generate unique versionCode based on minutes since epoch
        versionCode = (System.currentTimeMillis() / 60000).toInteger()
        // Set applicationId and app name based on build variant
        applicationId = buildVariant == 'release' ? "app.status.mobile" : "app.status.mobile.pr"
        resValue "string", "app_name", buildVariant == 'release' ? "Status" : "StatusPR"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("STATUS_APP_KEYSTORE_PATH") ?: "/nonexistent")
            storePassword System.getenv("STATUS_APP_KEYSTORE_PASSWORD")
            keyAlias System.getenv("STATUS_APP_KEY_ALIAS")
            keyPassword System.getenv("STATUS_APP_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            debuggable = false
            minifyEnabled = false
            def keystorePath = System.getenv("STATUS_APP_KEYSTORE_PATH")
            if (keystorePath && file(keystorePath).exists()) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable = true
        }
    }
}
